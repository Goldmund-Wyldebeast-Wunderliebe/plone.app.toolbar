*** Settings ***

Library  plone.act.PloneLibrary
Library  Selenium2Library  run_on_failure=Capture Page Screenshot
Variables  plone/app/testing/interfaces.py
Resource  plone/act/keywords.txt

Suite Setup  Open Plone Root

Suite Teardown  Close All Browsers

*** Test cases ***

Anonymous should'nt see toolbar
    Log out
    Page Should Not Contain Element  id=plone-toolbar

Logged in should see toolbar
    Log in as site owner
    Page Should Contain Element  id=plone-toolbar

Toolbar should contain content actions
    Log in as site owner
    Go to  ${PLONE_URL}

    Wait Until Page Contains Element  id=toolbar

    Frame Should Contain  id=toolbar  Contents
    Frame Should Contain  id=toolbar  View
    # The 'Edit' button is not present, as we are viewing a Plone Site, not
    #   a default front page for a Plone Site.
    Frame Should Contain  id=toolbar  Rules
    Frame Should Contain  id=toolbar  Sharing

Toolbar should contain document actions menus
    Log in as site owner
    Go to  ${PLONE_URL}

    Wait Until Page Contains Element  id=toolbar

    Frame Should Contain  id=toolbar  Display
    Frame Should Contain  id=toolbar  Add new

Contents tab should open folder listing
    Log in as site owner
    Go to  ${PLONE_URL}

    Wait Until Page Contains Element  id=toolbar
    Select Frame  id=toolbar
    Wait Until Keyword Succeeds  3  5  Current Frame Contains  Contents
    Click Link  Contents

    Location Should Contain  folder_contents
    
